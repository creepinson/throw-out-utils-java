plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id "com.jfrog.artifactory" version "4.17.2"
}

group 'dev.throwouterror'
version '1.0.0'

sourceCompatibility = targetCompatibility = 1.8

repositories {
    maven { url = "https://jitpack.io" }
    mavenCentral()
}

dependencies {
    compile "it.unimi.dsi:fastutil:8.3.1"
    compile 'com.google.code.gson:gson:2.8.6'
    compile 'org.easygson:easygson:1.3.1'
    compile "org.reflections:reflections:0.9.12"
    compile "com.fasterxml.jackson.core:jackson-databind:2.2.0"
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}
task packageSources(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    main.java.srcDirs += 'src/main/java'
}


artifacts {
    archives packageJavadoc
    archives packageSources
}

// Publishing stuff

publishing {
    publications {
        java(MavenPublication) {
            from components.java
            artifactId "throw-out-utils"
        }
    }
}

artifactory {
    contextUrl = 'https://repo.throw-out-error.dev/artifactory'
    publish {
        repository {
            repoKey = 'libs-release-local'

            username = artifactory_username
            password = artifactory_password
        }
        defaults {
            publications('java')
            publishArtifacts = true

            publishPom = true
        }
    }
}